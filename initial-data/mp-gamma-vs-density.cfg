#!/usr/local/bin/gnuplot

# load palette config
load '../styles.cfg'
# load presets
load '../run-paths-and-times.cfg'
load '../unit-conversions.cfg'

# parameters to configure margins of each graph
x0=0.1                      # left margin all graphs
y0=0.90                     # top margin all graphs
lengthx=0.4                 # horizonal length of each graph
lengthy=0.4                 # vertical length of each graph
spacex=0.00                 # horizontal space between each graph
spacey=0.00                 # vertical space between each graph

# This is a macro: configure the top, right, left, and bottom 
# margins of each graph.  It will depend on the value of i and j.
set macro
i=0; j=0;
SETMARGINS = "set tmargin at screen (y0-i*(lengthy+spacey));\
              set bmargin at screen (y0-i*(lengthy+spacey)-lengthy);\
              set lmargin at screen (x0+j*(lengthx+spacex));\
              set rmargin at screen (x0+j*(lengthx+spacex)+lengthx)"
POS = "at graph 0.92,0.9 font ',8'"
 
set tics scale 0.5
set ytics 1

# user defined macros
d2(x,y) = ($0 == 0) ? (x1 = x, y1 = y, 1/0) : (x2 = x1, x1 = x, y2 = y1, y1 = y, (y1-y2)/(x1-x2))
e=''
col1="$1*density_geo_cgs"
col2="d2(log($1),log($3))"


# Assuming all plots have the same x and y range. If not, set it in each plot below
min_rho_geo=1e-7
max_rho_geo=1e-2
set xrange [min_rho_geo*density_geo_cgs:max_rho_geo*density_geo_cgs]
set logscale x
min_gamma_geo=0
max_gamma_geo=4.8
set yrange [min_gamma_geo:max_gamma_geo]

# general setting for plot titles
set title offset -3,-3

# don't worry about the axis labels for now
unset xlabel
unset ylabel

#############################################################
set multiplot

  ##### top-left 
  i=0; j=0;
  @SETMARGINS                       # calling the macro for (i,j)=(0,0)
  set format y '%.0f'
  set format x ''

  set label 1 'a' @POS
  set key top left Left reverse font ',6'
  prefix='EOSVis/ploteos-'
  suffix='_ColdTable.dat'
  load 'plot-args.cfg'

  unset key
  prefix='Analysis/EOS/'

  ##### top-right
  i=0; j=1;
  @SETMARGINS                       # calling the macro for (i,j)=(0,1)
  set format xy ''                   # no numbers in x nor y-axis

  set label 1 'b' @POS
  suffix='_T1_Ye0.05.dat'
  load 'plot-args.cfg'

  ##### bottom-left
  i=1; j=0;
  @SETMARGINS                       # calling the macro for (i,j)=(1,0)
  set format y '%.0f'
  set format x '% g'
  set label 1 'c' @POS
  suffix='_T5_Ye0.05.dat'
  load 'plot-args.cfg'

  ##### bottom-right
  i=1; j=1;
  @SETMARGINS                       # calling the macro for (i,j)=(1,1)
  set format y ''                   # no numbers in y-axis

  # now we set the x and y labels for the 4th plot, and move 
  # them to the desired position
  set xlabel '$\rho_0$ (g/cm$^3$)' offset screen -0.5*(spacex+lengthx),0.0
  set ylabel '$\tilde{\Gamma}$' offset screen -lengthx-1.5*spacex,0.5*(spacey+lengthy)

  set label 1 'd' @POS
  suffix='_T10_Ye0.05.dat'
  load 'plot-args.cfg'

unset multiplot

reset

load 'unit-conversions.gpi'

# Inverse square law
F(x) = M_sun_mks*G_mks/x**2

set title 'Force on a 1 kg particle passing straight through the sun, if it were uniformly dense'
set xlabel 'Radial position (m)'
set x2label 'Radial position (G=c=M_sun=1)'
set ylabel 'Force (N)'
set y2label 'Force (G=c=M_sun=1)'

min_R = R_sun_mks*1e-1
max_R = R_sun_mks
set xrange [min_R:max_R]
set x2range [min_R*length_mks_geo:max_R*length_mks_geo]
min_F = 1e-1
max_F = 3e4
force_mks_geo = mass_mks_geo*length_mks_geo/time_mks_geo**2
set yrange [min_F:max_F]
set y2range [min_F*force_mks_geo:max_F*force_mks_geo]

set xtics nomirror
set x2tics nomirror
set ytics nomirror
set y2tics nomirror 

set format x "%3.2e"
set format y2 "%3.2e"

plot F(x) t 'F(r) = G M m / r^2 '
